@name VOX_TTS
@persist E2IsSpeaking SoundQueue:array ThisSound:string
runOnChat(1)

function void speakVox(Text:string) {
    local TextArray = Text:explode(" ")
    foreach (K, V:string=TextArray) {
        SoundQueue:pushString("vox/" + V + ".wav")
    }
    if (E2IsSpeaking == 0) {
        timer("vox", 2)
        E2IsSpeaking = 1
    }
    else {
        SoundQueue:pushString("vox/_period.wav")
    }
}

if (first()) {
    SoundQueue = array()
    E2IsSpeaking = 0
    #speakVox("vox_login")
}

if (clkName() == "vox") {
    if (SoundQueue:count() > 0) {
        ThisSound = SoundQueue:shiftString()
        soundPlay(1, 0, ThisSound)
        stoptimer("vox")
        if (SoundQueue:count() > 0) {
            timer("vox", soundDuration(ThisSound) * 1000)
        }
        else {
            E2IsSpeaking = 0
        }
    }
}

if (chatClk(owner())) {
    OwnerLastSaid = owner():lastSaid():explode(" ")
    if (OwnerLastSaid:shiftString() == "/vox") {
        hideChat(1)
        speakVox(OwnerLastSaid:concat(" "))
    }
}

# Built by Zorb, November 27th 2019