@name World's Greatest Moped
@model models/props_junk/wood_crate002a.mdl

# Static

@persist [Owner Entity Driver Seat]:entity

# Holos

@persist Idx
@persist HOLO_BASE HOLO_HANDLE HOLO_FRONT_WHEEL HOLO_BACK_WHEEL
@persist HOLO_STEERING_COLUMN HOLO_PAN HOLO_LICENSE
@persist HOLO_SOUND1 HOLO_SOUND2 LIGHTS:array

@persist [ENTITY_BASE ENTITY_SOUND2 ENTITY_COLUMN ENTITY_FLOOR]:entity

# Sound

@persist SoundSwap

# Misc

@persist COLOR_BASE:vector TEXTURE_BASE:string
@persist Tick AvgSpeed:array LastSpeed TurnTick Grounded

# Control

@persist TurnLeft TurnRight Forward Backward Turn Brake Lights Shift

interval(60)

Tick++

if (first()) {
    # models/Mechanics/wheels/wheel_race.mdl
    # models/props_junk/bicycle01a.mdl
    
    propSpawnUndo(0)
    runOnLast(1)
    
    Owner = owner()
    Entity = entity()
    AvgSpeed = array()
    COLOR_BASE = vec(255)
    TEXTURE_BASE = "models/props/cs_assault/metal_stairs1"
    
    # Functions
    
    function number swivel(A:number,B:number) {
        local C1=(B+(360-A))%360
        local C2=C1-360
        return abs(C1)<abs(C2) ? C1 : C2
    }
    
    function angle swivel(A:angle,B:angle) {
        return ang(
            swivel(A:pitch(),B:pitch()),
            swivel(A:yaw(),B:yaw()),
            swivel(A:roll(),B:roll())
        )
    }
    
    # Base
    Idx++
    HOLO_BASE = Idx
    HOLO_SOUND1 = Idx
    holoCreate(Idx, vec(), vec())
    ENTITY_BASE = holoEntity(Idx)
    
    Idx++
    holoCreate(Idx, vec(), vec())
    holoParent(Idx, HOLO_BASE)
    HOLO_SOUND2 = Idx
    ENTITY_SOUND2 = holoEntity(Idx)
    
    # Steering column
    Idx++
    holoCreate(Idx, vec(31, 0, -70), vec(0.8, 0.8, 1), ang(), vec(255), "models/props_c17/signpole001.mdl")
    holoParent(Idx, HOLO_BASE)
    
    holoClip(Idx, 1, vec(0, 0, 84), vec(0, 0, 1), 0)
    holoClipEnabled(Idx, 1, 1)
    
    HOLO_STEERING_COLUMN = Idx
    ENTITY_COLUMN = holoEntity(Idx)
    
    # Front wheel
    Idx++
    holoCreate(Idx, vec(40, 0, 0), vec(0.1), ang(0, 0, 0), vec(255), "models/props_wasteland/wheel03a.mdl")
    holoParent(Idx, HOLO_STEERING_COLUMN)
    HOLO_FRONT_WHEEL = Idx
    
    # Front wheel cover
    Idx++
    holoCreate(Idx, vec(49, 0, 7.5), vec(0.6, 0.6, 0.5), ang(232, 0, 0), COLOR_BASE * 0.8, "models/combine_helicopter/bomb_debris_2.mdl")
    holoMaterial(Idx, TEXTURE_BASE)
    holoParent(Idx, HOLO_STEERING_COLUMN)
    
    # back
    Idx++
    holoCreate(Idx, vec(31, 0, 8), vec(0.8, 0.7, 0.5), ang(135, 0, 0), COLOR_BASE * 0.8, "models/combine_helicopter/bomb_debris_2.mdl")
    holoMaterial(Idx, TEXTURE_BASE)
    holoParent(Idx, HOLO_STEERING_COLUMN)
    
    # mid
    Idx++
    holoCreate(Idx, vec(39.5, 0, 12), vec(0.6, 0.7, 0.5), ang(180, 0, 0), COLOR_BASE * 0.8, "models/combine_helicopter/bomb_debris_2.mdl")
    holoParent(Idx, HOLO_STEERING_COLUMN)
    holoMaterial(Idx, TEXTURE_BASE)
    
    # Axle holders
    
    Idx++
    holoCreate(Idx, vec(35.25, 3.75, 4.6), vec(1), ang(315, 0, 90), vec(255), "models/props_c17/tools_wrench01a.mdl")
    holoParent(Idx, HOLO_STEERING_COLUMN)
    
    holoClip(Idx, 1, vec(0, 7, 0), vec(0, -1, 0), 0)
    holoClipEnabled(Idx, 1, 1)
    
    holoMaterial(Idx, "phoenix_storms/iron_rails")
    
    Idx++
    holoCreate(Idx, vec(35.25, -3.25, 4.6), vec(1), ang(315, 0, 90), vec(255), "models/props_c17/tools_wrench01a.mdl")
    holoParent(Idx, HOLO_STEERING_COLUMN)
    
    holoClip(Idx, 1, vec(0, 7, 0), vec(0, -1, 0), 0)
    holoClipEnabled(Idx, 1, 1)
    
    holoMaterial(Idx, "phoenix_storms/iron_rails")
    
    # Axle
    Idx++
    
    holoCreate(Idx, vec(40, 5, 0), vec(0.5, 0.5, 0.1), ang(0, 0, 90), vec(255), "models/props_c17/signpole001.mdl")
    holoParent(Idx, HOLO_STEERING_COLUMN)
    
    # Front bit
    
    Idx++
    
    holoCreate(Idx, vec(35, 0, 20), vec(0.2, 0.5, 1), ang(25, 180, 0), COLOR_BASE * 0.8, "models/props_interiors/Furniture_Couch02a.mdl")
    holoParent(Idx, HOLO_STEERING_COLUMN)
    holoMaterial(Idx, TEXTURE_BASE)
    
    holoClip(Idx, 1, vec(0, 0, -5), vec(0, 0, 1), 0)
    holoClipEnabled(Idx, 1,1)
    
    Idx++
    
    holoCreate(Idx, vec(31, 0, 17), vec(0.2, 0.5, 0.7), ang(235, 0, 0), COLOR_BASE * 0.8, "models/props_interiors/Furniture_Couch02a.mdl")
    holoParent(Idx, HOLO_STEERING_COLUMN)
    holoMaterial(Idx, TEXTURE_BASE)
    
    holoClip(Idx, 1, vec(0, 0, -5), vec(0, 0, 1), 0)
    holoClipEnabled(Idx, 1,1)
    
    holoClip(Idx, 2, vec(0, 0, 9), vec(0, 0, -1), 0)
    holoClipEnabled(Idx, 2, 1)
    
    Idx++
    holoCreate(Idx, vec(31, 0, 15), vec(0.2, 0.2, 0.1), ang(135, 0, 0), COLOR_BASE * 0.8, "models/props_c17/oildrum001.mdl")
    holoParent(Idx, HOLO_STEERING_COLUMN)
    holoMaterial(Idx, TEXTURE_BASE)
    
    # Handles & light
    Idx++
    HOLO_HANDLE = Idx
    holoCreate(Idx, vec(40, 0, 22), vec(1), ang(0, 180, 0), COLOR_BASE * 0.8, "models/props_junk/bicycle01a.mdl")
    holoParent(Idx, HOLO_STEERING_COLUMN)
    holoMaterial(Idx, TEXTURE_BASE)
    
    holoClip(Idx, 1, vec(), vec(1, 0, 0), 0)
    holoClipEnabled(Idx, 1, 1)
    
    holoClip(Idx, 2, vec(0, 0, 16), vec(0, 0, 1), 0)
    holoClipEnabled(Idx, 2, 1)
    
    Idx++
    holoCreate(Idx, vec(28, 0, 42), vec(0.8), ang(), vec(255), "models/props_wasteland/light_spotlight02_lamp.mdl")
    holoParent(Idx, HOLO_HANDLE)
    holoSkin(Idx, 1)
    
    lightCreate(1, holoEntity(Idx):pos() + vec(10, 0, 0), vec(128), 32, 8)
    lightParent(1, holoEntity(Idx))
    LIGHTS:pushNumber(Idx)
    
    # Base floorboard
    Idx++
    holoCreate(Idx, vec(12, 0, -1), vec(1.5, 0.75, 0.4), ang(0, 180, 0), COLOR_BASE * 0.8, "models/props_lab/citizenradio.mdl")
    holoParent(Idx, HOLO_BASE)
    holoMaterial(Idx, TEXTURE_BASE)
    
    Idx++
    holoCreate(Idx, vec(12, 0, 0), vec(1.49, 0.74, 0.39), ang(0, 180, 0), COLOR_BASE * 0.8, "models/props_lab/citizenradio.mdl")
    holoParent(Idx, HOLO_BASE)
    holoMaterial(Idx, "models/gibs/metalgibs/metal_gibs")
    
    # Seat cover
    Idx++
    holoCreate(Idx, vec(-8, 0, 14), vec(1, 0.74, 0.4), ang(0, 180, 0), COLOR_BASE * 0.8, "models/props_lab/citizenradio.mdl")
    holoParent(Idx, HOLO_BASE)
    holoMaterial(Idx, TEXTURE_BASE)
    
    Idx++
    holoCreate(Idx, vec(-8, 0, 14.5), vec(1, 0.74, 0.4) * 0.98, ang(0, 180, 0), vec(255, 0, 0), "models/props_lab/citizenradio.mdl")
    holoParent(Idx, HOLO_BASE)
    holoMaterial(Idx, "models/props_c17/FurnitureFabric003a")
    
    # Back wheel
    Idx++
    holoCreate(Idx, vec(-15, 0, 2), vec(0.11), ang(0, 0, 0), vec(255), "models/props_wasteland/wheel03a.mdl")
    holoParent(Idx, HOLO_BASE)
    HOLO_BACK_WHEEL = Idx
    
    # 15, 0, 2
    
    Idx++
    holoCreate(Idx, vec(-4, 0, 8.5), vec(0.6, 0.6, 0.5) * 1.1, ang(240, 0, 0), COLOR_BASE * 0.8, "models/combine_helicopter/bomb_debris_2.mdl")
    holoMaterial(Idx, TEXTURE_BASE)
    holoParent(Idx, HOLO_BASE)
    
    # back
    Idx++
    holoCreate(Idx, vec(-26, 0, 9), vec(0.8, 0.7, 0.5) * 1.1, ang(135, 0, 0), COLOR_BASE * 0.8, "models/combine_helicopter/bomb_debris_2.mdl")
    holoMaterial(Idx, TEXTURE_BASE)
    holoParent(Idx, HOLO_BASE)
    
    # mid
    Idx++
    holoCreate(Idx, vec(-15, 0, 14), vec(0.6, 0.7, 0.5) * 1.1, ang(180, 0, 0), COLOR_BASE * 0.8, "models/combine_helicopter/bomb_debris_2.mdl")
    holoParent(Idx, HOLO_BASE)
    holoMaterial(Idx, TEXTURE_BASE)
    
    # Back lights
    Idx++
    holoCreate(Idx, vec(-31.5, 5, 5), vec(0.5), ang(0, 180, 0), vec(255), "models/props_wasteland/light_spotlight02_lamp.mdl")
    holoParent(Idx, HOLO_BASE)
    holoSkin(Idx, 1)
    
    lightCreate(2, holoEntity(Idx):pos() - vec(5, 0, 0), vec(128), 32, 8)
    lightParent(2, holoEntity(Idx))
    
    LIGHTS:pushNumber(Idx)
    
    Idx++
    holoCreate(Idx, vec(-31.5, -5, 5), vec(0.5), ang(0, 180, 0), vec(255), "models/props_wasteland/light_spotlight02_lamp.mdl")
    holoParent(Idx, HOLO_BASE)
    holoSkin(Idx, 1)
    
    lightCreate(3, holoEntity(Idx):pos() - vec(5, 0, 0), vec(128), 32, 8)
    lightParent(3, holoEntity(Idx))
    
    LIGHTS:pushNumber(Idx)
    
    # models/props_trainstation/payphone_reciever001a.mdl
    
    Idx++
    holoCreate(Idx, vec(9, -3, 2), vec(1), ang(0, 255, 90), vec(255), "models/props_trainstation/payphone_reciever001a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    Idx++
    holoCreate(Idx, vec(-9, -6, 2), vec(1), ang(90, 0, 0), vec(255), "models/props_c17/signpole001.mdl")
    holoParent(Idx, HOLO_BASE)
    holoClip(Idx, 1, vec(0, 0, 15), vec(0, 0, -1), 0)
    holoClipEnabled(Idx, 1, 1)
    
    # Engine
    Idx++
    holoCreate(Idx, vec(-15, 8, 2), vec(0.8), ang(90, 90, 0), vec(255), "models/props_junk/metal_paintcan001a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    Idx++
    holoCreate(Idx, vec(16, -2, -2), vec(0.8), ang(0, 180, 180), vec(255), "models/props_vehicles/carparts_muffler01a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    holoClip(Idx, 1, vec(0, 0, 0), vec(1, 0, 0), 0)
    holoClipEnabled(Idx, 1, 1)
    
    Idx++
    holoCreate(Idx, vec(16, -8, -1.5), vec(0.2), ang(270, 270, 0), vec(255), "models/props_pipes/pipe03_90degree01.mdl")
    holoParent(Idx, HOLO_BASE)
    holoSkin(Idx, 1)
    
    # Bin
    Idx++
    holoCreate(Idx, vec(-28, 0, 25), vec(1), ang(), vec(255), "models/props_junk/PlasticCrate01a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    Idx++
    holoCreate(Idx, vec(-28, 0, 25), vec(1), ang(), vec(255), "models/props_junk/watermelon01.mdl")
    holoParent(Idx, HOLO_BASE)
    
    Idx++
    holoCreate(Idx, vec(-35, 0, 27), vec(0.8), ang(85, 0, 0), vec(255), "models/props_junk/garbage_newspaper001a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    Idx++
    holoCreate(Idx, vec(-28, 0, 19), vec(1), ang(), vec(255), "models/dynamite/dynamite.mdl")
    holoParent(Idx, HOLO_BASE)
    
    Idx++
    holoCreate(Idx, vec(-28, -11.5, 9), vec(1), ang(180, 0, 90), vec(255), "models/props_interiors/pot02a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    Idx++
    holoCreate(Idx, vec(-28, 18, 28), vec(0.7), ang(), vec(255), "models/props_junk/PropaneCanister001a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    Idx++
    holoCreate(Idx, vec(-28, 14, 20), vec(1), ang(270, 180, 90), vec(255), "models/props_wasteland/panel_leverHandle001a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    Idx++
    holoCreate(Idx, vec(-28, 11, 85), vec(1), ang(0, -30, 0), vec(255), "models/props_c17/gaspipes004a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    holoClip(Idx, 1, vec(0, 0, -60), vec(0, 0, -1), 0)
    holoClipEnabled(Idx, 1, 1)
    
    Idx++
    holoCreate(Idx, vec(-7.5, -15, 12), vec(0.2), ang(10, -90, 0), vec(255), "models/props_junk/trashdumpster01a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    # Side pipes
    
    Idx++
    holoCreate(Idx, vec(5, 8, 2), vec(0.6, 0.6, 1), ang(90, 0, 0), vec(255), "models/props_junk/propane_tank001a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    Idx++
    holoCreate(Idx, vec(-2, 4, 4), vec(1, 1, 0.5), ang(0, 85, -90), vec(255), "models/props_c17/gaspipes006a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    # Globe
    Idx++
    holoCreate(Idx, vec(34.5, 0, 26.5), vec(0.75, 0.75, 0.25), ang(67, 0, 0), COLOR_BASE * 0.8, "models/props_combine/breenglobe.mdl")
    holoParent(Idx, HOLO_STEERING_COLUMN)
    holoMaterial(Idx, TEXTURE_BASE)
    
    # Chair back
    
    Idx++
    holoCreate(Idx, vec(-8, 0, 28), vec(1, 1, 0.8), ang(-7.5, 0, 0), vec(255), "models/props_c17/furniturechair001a_chunk01.mdl")
    holoParent(Idx, HOLO_BASE)
    
    holoClip(Idx, 1, vec(0, 0, -8), vec(0, 0, 1), 0)
    holoClipEnabled(Idx, 1, 1)
    
    # Armrests
    Idx++
    holoCreate(Idx, vec(-8, -1.8, 13), vec(1, 1, 0.8), ang(-5, 90, 0), vec(255), "models/props_interiors/furniture_chair03a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    holoClip(Idx, 1, vec(0, 0, 2.1), vec(0, 0, 1), 0)
    holoClipEnabled(Idx, 1, 1)
    
    Idx++
    holoCreate(Idx, vec(-8, 1.8, 13), vec(1, 1, 0.8), ang(-5, -90, 0), vec(255), "models/props_interiors/furniture_chair03a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    holoClip(Idx, 1, vec(0, 0, 2.1), vec(0, 0, 1), 0)
    holoClipEnabled(Idx, 1, 1)
    
    # Cup holders
    Idx++
    holoCreate(Idx, vec(-5, 14, 25), vec(0.3, 0.3, 0.3), ang(0, 90, 0), vec(255), "models/props_junk/MetalBucket01a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    Idx++
    holoCreate(Idx, vec(-5, 14, 26), vec(1), ang(0, 90, 0), vec(255), "models/props_junk/PopCan01a.mdl")
    holoParent(Idx, HOLO_BASE)
    holoSkin(Idx, 1)
    
    Idx++
    holoCreate(Idx, vec(-12, 14, 25), vec(0.3, 0.3, 0.3), ang(0, 90, 0), vec(255), "models/props_junk/MetalBucket01a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    # Front monitor
    Idx++
    holoCreate(Idx, vec(28, 0, 28), vec(1), ang(-30, 180, 0), vec(255), "models/props_lab/monitor01b.mdl")
    holoParent(Idx, HOLO_STEERING_COLUMN)
    
    Idx++
    holoCreate(Idx, vec(21.75, -5, 29), vec(0.1), ang(-60, 0, 0), vec(255), "models/maxofs2d/button_01.mdl")
    holoParent(Idx, HOLO_STEERING_COLUMN)
    
    # Base Computer
    Idx++
    holoCreate(Idx, vec(12, 4.6, 2.5), vec(1), ang(0, 90, 0), vec(255), "models/props_lab/reciever01a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    # Rack
    Idx++
    holoCreate(Idx, vec(-14, 0, 23), vec(1, 1.3, 1), ang(-87, 0, 0), COLOR_BASE * 0.8, "models/props_interiors/Furniture_chair03a.mdl")
    holoParent(Idx, HOLO_BASE)
    holoMaterial(Idx, TEXTURE_BASE)
    
    holoClip(Idx, 1, vec(0, 0, 1.4), vec(0, 0, 1), 0)
    holoClipEnabled(Idx, 1, 1)
    
    # Base -> front wheel
    Idx++
    holoCreate(Idx, vec(12, 0, -2), vec(1), ang(-125, 0, 0), COLOR_BASE * 0.8, "models/props_lab/lockerdoorleft.mdl")
    holoMaterial(Idx, TEXTURE_BASE)
    holoParent(Idx, HOLO_BASE)
    
    holoClip(Idx, 1, vec(0, 0, -3), vec(0, 0, -1), 0)
    holoClipEnabled(Idx, 1, 1)
    
    # Base -> back wheel
    Idx++
    holoCreate(Idx, vec(-9, 0, 18), vec(1), ang(-10, 0, 0), vec(255), "models/props_interiors/Furniture_chair03a.mdl")
    holoParent(Idx, HOLO_BASE)
    
    holoClip(Idx, 1, vec(0, 0, -3), vec(0, 0, -1), 0)
    holoClipEnabled(Idx, 1, 1)
    
    holoClip(Idx, 2, vec(), vec(1, 0, 0), 0)
    holoClipEnabled(Idx, 2, 1)
    
    # "License" plate
    
    Idx++
    holoCreate(Idx, vec(-36.5, 0, 12.5), vec(0.25, 0.4, 0.2), ang(180, 0, 0), vec(255), "models/props_c17/FurnitureDrawer001a_Chunk05.mdl")
    holoParent(Idx, HOLO_BASE)
    holoMaterial(Idx, TEXTURE_BASE)
    
    Idx++
    holoCreate(Idx, vec(-33.6, 0, 9), vec(0.4, 0.1, 0.4), ang(0, 90, 0), vec(0, 255, 0), "models/player/items/humans/top_hat.mdl")
    holoParent(Idx, HOLO_BASE)
    
    Idx++
    holoCreate(Idx, vec(-33.6, -5, 9), vec(0.4, 0.1, 0.4), ang(0, 90, 0), vec(0, 0, 255), "models/player/items/humans/top_hat.mdl")
    holoParent(Idx, HOLO_BASE)
    
    Idx++
    holoCreate(Idx, vec(-33.6, 5, 9), vec(0.4, 0.1, 0.4), ang(0, 90, 0), vec(255, 0, 0), "models/player/items/humans/top_hat.mdl")
    holoParent(Idx, HOLO_BASE)
    
    # Entity=seatSpawn(string model,vector pos,angle rot,number frozen)
    # models/nova/chair_wood01.mdl
    
    Seat = seatSpawn("models/nova/chair_wood01.mdl", vec(-12, 0, 6), ang(0, -90, 0), 1)
    Seat:setAlpha(0)
    
    #ifdef luaRun(string)
        luaRun(format("Entity(%d):keysOwn(Entity(%d))",Seat:id(), Owner:id()))
    #endif
    
    # Hitbox
    local HitboxBase = propSpawn("models/hunter/blocks/cube075x075x025.mdl", vec(15, 0, 3), ang(), 1)
    HitboxBase:setAlpha(0)
    HitboxBase:setMass(50000)
    Seat:parentTo(HitboxBase)
    
    ENTITY_FLOOR = HitboxBase
    
    local HitboxFrontWheel = propSpawn("models/hunter/blocks/cube05x05x025.mdl", vec(40, 0, 3), ang(0, 0, 90), 1)
    HitboxFrontWheel:parentTo(HitboxBase)
    HitboxFrontWheel:setAlpha(0)
    
    local HitboxFront = propSpawn("models/hunter/plates/plate075x075.mdl", vec(31, 6, 35), ang(-115, 0, 0), 1)
    HitboxFront:parentTo(HitboxBase)
    HitboxFront:setAlpha(0)
    
    local HitboxBackWheel = propSpawn("models/hunter/blocks/cube075x075x025.mdl", vec(-15, 0, 10), ang(0, 0, 90), 1)
    HitboxBackWheel:parentTo(HitboxBase)
    HitboxBackWheel:setAlpha(0)
    
    local HitboxBack = propSpawn("models/hunter/blocks/cube05x1x05.mdl", vec(-30, 0, 30), ang(), 1)
    HitboxBack:parentTo(HitboxBase)
    HitboxBack:setAlpha(0)
    
    # Repos moped
    
    Seat:propFreeze(0)
    HitboxBase:propFreeze(0)
    HitboxFrontWheel:propFreeze(0)
    HitboxFront:propFreeze(0)
    HitboxBackWheel:propFreeze(0)
    HitboxBack:propFreeze(0)
    
    holoParent(HOLO_BASE, HitboxBase)
    HitboxBase:setPos(Entity:toWorld(vec(0, 0, 30)))
    HitboxBase:setAng(Entity:angles() + ang(0, 90, 0))
    
    for (I = 1, 3) {
        lightToggle(I, Lights)
    }
    
    propSpawn("models/props_junk/wood_crate002a.mdl", Entity:toWorld(vec(0, 0, 30)), Entity:angles(), 1):propBreak()
    
    Entity:propNotSolid(1)
    Entity:setPos(vec(0, 0, -500))
} elseif (last()) {
    soundPurge()
    lightRemoveAll()
    holoDeleteAll()
} elseif (clk()) {
    Driver = Seat:driver()
    
    local Starting = changed(Driver) & Driver
    local Stopping = changed(Driver) & !Driver
    
    if (Starting) {
        holoEntity(HOLO_BASE):soundPlay("start", 1.35, "vehicles/airboat/fan_motor_start1.wav")
        soundPitch("start", 80)
        soundVolume("start", 50)
        
        runOnKeys(Driver, 1)
    }
    
    if (Stopping) {
        holoEntity(HOLO_BASE):soundPlay("stop", 2, "vehicles/airboat/fan_motor_shut_off1.wav")
        soundPitch("stop", 80)
        soundStop("motor")
        
        foreach (I, V:entity = players()) {
            runOnKeys(V, 0)
        }
    }
    
    if (Starting | (Tick % 100 == 0 & Driver)) {
        SoundSwap = !SoundSwap
        holoEntity(SoundSwap ? HOLO_SOUND1 : HOLO_SOUND2):soundPlay("motor", 6, "vehicles/v8/v8_rev_short_loop1.wav")
    }
    
    local HorizVel = ENTITY_FLOOR:vel() * vec(1, 1, 0)
    
    AvgSpeed:unshiftNumber(HorizVel:length())
    
    if (AvgSpeed:count() > 5) {
        AvgSpeed:popNumber()
    }
    
    rangerDefaultZero(1)
    local Start = ENTITY_FLOOR:toWorld(ENTITY_FLOOR:boxCenter() - ENTITY_FLOOR:aabbSize() * vec(0, 0, 0.5) - vec(0, 30, -25))
    local Start2 = ENTITY_FLOOR:toWorld(ENTITY_FLOOR:boxCenter() - ENTITY_FLOOR:aabbSize() * vec(0, 0, 0.5) - vec(0, 0, 5))
    local Start3 = ENTITY_FLOOR:toWorld(ENTITY_FLOOR:boxCenter() - ENTITY_FLOOR:aabbSize() * vec(0, 0, 0.5) - vec(0, -30, -25))
    local Down = rangerOffsetHull(Start, Start - vec(0, 0, 500), vec(5))
    local Down2 = rangerOffsetHull(Start, Start2 - vec(0, 0, 500), vec(5))
    local Down3 = rangerOffsetHull(Start, Start3 - vec(0, 0, 500), vec(5))
    
    local Ranger = ranger(1)
    
    local MinDist = 0
    
    if (Down:distance() & Down:distance() > MinDist) {
        Ranger = Down
        MinDist = Down:distance()
    }
    
    if (Down2:distance() & Down2:distance() > MinDist) {
        MinDist = Down2:distance()
        Ranger = Down2
    }
    
    if (Down3:distance() & Down3:distance() > MinDist) {
        MinDist = Down3:distance()
        Ranger = Down3
    }
    
    holoPos(200, Start)
    holoPos(201, Start2)
    holoPos(202, Start3)
    
    local DownDist = ENTITY_FLOOR:pos():z() - Ranger:pos():z()
    
    Grounded = Ranger:hit() & Ranger:distance() < 50 || DownDist == 0
    
    if (!Ranger) {
        Ranger = Down
        Grounded = 0
    }
    
    local Speed = Grounded ? AvgSpeed:average() / 3 : LastSpeed * 0.9
    
    # Update motor pitch
    
    soundPitch("motor", clamp(Speed, 80, 200))
    
    # Animate wheels
    
    local MoveDir = sign(ENTITY_FLOOR:toLocal(ENTITY_FLOOR:vel() + ENTITY_FLOOR:pos()):x())
    TurnTick +=  MoveDir * Speed
    
    LastSpeed = Speed
    
    Turn = clamp(Turn + TurnLeft * 0.2 + TurnRight * -0.2, -1, 1) * 0.8
    
    holoAng(HOLO_STEERING_COLUMN, ENTITY_BASE:toWorld(ang(0, Turn * 30, 0)))
    
    holoAng(HOLO_FRONT_WHEEL, ENTITY_COLUMN:toWorld(ang(TurnTick / 4, 0, 0)))
    holoAng(HOLO_BACK_WHEEL, ENTITY_BASE:toWorld(ang(TurnTick / 4, 0, 0)))
    
    # Disable gravity to conserve ops
    
    ENTITY_FLOOR:propGravity(!Grounded)
    
    # Fake air resistance
    
    ENTITY_FLOOR:applyAngForce(-ENTITY_FLOOR:angVel() * ENTITY_FLOOR:mass() * 0.8)
    
    # Keep upright
    
    TurnRatio = clamp(Speed / 50, 0, 1)
    
    local Normal = Ranger:hitNormal()
    local GroundAng = Normal:toAngle()
    local Unclamped = toWorldAng(vec(), ang(90, 0, 0), vec(), GroundAng):setYaw(ENTITY_FLOOR:angles():yaw())
    
    holoAng(202, Unclamped)
    
    local FinalAng = clamp(Unclamped, ang(-15), ang(15))
        FinalAng = toWorldAng(vec(), ang(0, 0, TurnRatio * Turn * -15), vec(), FinalAng) # tilt when turning
        
    ENTITY_FLOOR:applyAngForce((swivel(ENTITY_FLOOR:angles(), FinalAng)) * ENTITY_FLOOR:mass() * ang(1, 0, 1))
    
    # Turning as a function of speed
    
    ENTITY_FLOOR:applyAngForce(ang(0, MoveDir * Turn * 80 * TurnRatio * ENTITY_FLOOR:mass(), 0))
    
    # Braking
    
    ENTITY_FLOOR:applyForce(-ENTITY_FLOOR:vel() * ENTITY_FLOOR:mass() * (Brake ? vec(0.1, 0.1, Grounded) : vec(0.01, 0.01, Grounded)))
    
    # Directional momentum damping for realism
    
    local BaseVel=ENTITY_FLOOR:vel()
    local Center=ENTITY_FLOOR:pos()
    local DampingAxes=vec(0.01, 0.95, 0)
    
    local DampingOffset=
        ENTITY_FLOOR:toWorld(ENTITY_FLOOR:toLocal(Center + BaseVel) * DampingAxes) - Center
        
    ENTITY_FLOOR:applyForce(-DampingOffset * ENTITY_FLOOR:mass())
    
    # Grounding simulation and controls
    
    if (Grounded) {
        ENTITY_FLOOR:applyForce(vec(0, 0, (13 + Normal:length())  - DownDist) * ENTITY_FLOOR:mass() * 2)
        ENTITY_FLOOR:applyForce((ENTITY_FLOOR:forward() * Forward * 10 - ENTITY_FLOOR:forward() * Backward * 10) * ENTITY_FLOOR:mass() * Shift)
    }
}

if (keyClk(Driver) || changed(Driver)) {
    Brake = Driver:keyJump()
    
    TurnLeft = Driver:keyLeft()
    TurnRight = Driver:keyRight()
    Forward = Driver:keyForward() & !Brake
    Backward = Driver:keyBack() & !Brake
    Shift = (Driver:keySprint() & !Brake) ? 10 : 1
    
    if (Driver:keyPressed("f")) {
        Lights = !Lights
        
        foreach (I, V:number = LIGHTS) {
            holoSkin(V, !Lights)
        }
        
        for (I = 1, 3) {
            lightToggle(I, Lights)
        }
    }
    
    if (Driver:keyReload()) {
        ENTITY_FLOOR:soundPlay("horn", 0.6, "ambient/mvm_warehouse/car_horn_02.wav")
        soundPitch("horn", 80)
    }
}