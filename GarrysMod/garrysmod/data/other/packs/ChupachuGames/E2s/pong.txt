
### ### ### ### ### ###
### Made By Perli55 ###
### ### ### ### ### ###
 
@name Pong
@inputs EGP:wirelink Pod1:wirelink Pod2:wirelink
@outputs Max Move Go:vector2 Ball:vector2 P1:vector2 P2:vector2
@outputs DL:vector2 DR:vector2 UL:vector2 UR:vector2 
@outputs W1 S1 A1 D1 W2 S2 A2 D2 Boost Score1 Score2 Dirs:array
if (duped()) {reset()}
if (first()) {    
    DL=vec2(-1,1),DR=vec2(1,1),UL=vec2(-1,-1),UR=vec2(1,-1)
    #Down-Left,Down-Right,Up-Left,Up-Right
    
    EGP:egpClear()
    
    EGP:egpBox(100,vec2(255,255),vec2(520,510))
    EGP:egpColor(100,vec(100,100,100))
    
    EGP:egpCircle(1,vec2(255,255),vec2(13,13))
    EGP:egpColor(1,vec(0,0,200))
    Dirs=array(DL,UL,DR,UR)
    Go=Dirs[randint(1,4),vector2]
    
    EGP:egpBox(2,vec2(30,255),vec2(20,80))
    EGP:egpColor(2,vec(100,0,0))
    
    EGP:egpBox(3,vec2(480,255),vec2(20,80))
    EGP:egpColor(3,vec(100,0,0))
    
    EGP:egpBox(4,vec2(255,0),vec2(520,20))
    EGP:egpColor(4,vec(0,150,20))
    
    EGP:egpBox(5,vec2(255,510),vec2(520,20))
    EGP:egpColor(5,vec(0,150,20))
    
    EGP:egpBox(6,vec2(255,250),vec2(160,146))
    EGP:egpColor(6,vec(255,122,0))
    EGP:egpText(7,"Pong",vec2(190,175))
    EGP:egpSize(7,50)
    EGP:egpText(8,"Player 1's Score: 0",vec2(180,230))    
    EGP:egpText(9,"Player 2's Score: 0",vec2(180,260))    
    EGP:egpText(10,"Speed: 30%",vec2(180,290))
    EGP:egpAlpha(6,100)
    EGP:egpAlpha(7,170)
    EGP:egpAlpha(8,170)
    EGP:egpAlpha(9,170)
    EGP:egpAlpha(10,170)
        
    Max=egpMaxUmsgPerSecond()
    if (Max>30) {Max=30}
    Move=(1000/Max)/8
    Boost=1.5
    }
interval(1000/Max)

#This Part Makes The Ball Bounce Agains The 2 Walls And The 2 Pedals ,P1 and P2 Are Theyre Positions
Ball=EGP:egpPos(1)
if (Go==DL&inrange(Ball+vec2(0,13),vec2(0,500),vec2(600,600))) {Go=UL}
if (Go==DR&inrange(Ball+vec2(0,13),vec2(0,500),vec2(600,600))) {Go=UR}
if (Go==UL&inrange(Ball+vec2(0,-13),vec2(-100,-100),vec2(510,10))) {Go=DL}
if (Go==UR&inrange(Ball+vec2(0,-13),vec2(-100,-100),vec2(510,10))) {Go=DR}
P1=EGP:egpPos(2)
if (Go==UL&inrange(Ball+vec2(-13,0),P1+vec2(-10,-40),P1+vec2(10,40))) {Go=UR,Boost+=0.1}
if (Go==DL&inrange(Ball+vec2(-13,0),P1+vec2(-10,-40),P1+vec2(10,40))) {Go=DR,Boost+=0.1}
if (Go==UL&inrange(Ball+vec2(0,-13),P1+vec2(-10,-40),P1+vec2(10,40))) {Go=DR,Boost+=0.1}
if (Go==DL&inrange(Ball+vec2(0,13),P1+vec2(-10,-40),P1+vec2(10,40))) {Go=UR,Boost+=0.1}
P2=EGP:egpPos(3)
if (Go==UR&inrange(Ball+vec2(13,0),P2+vec2(-10,-40),P2+vec2(10,40))) {Go=UL,Boost+=0.1}
if (Go==DR&inrange(Ball+vec2(13,0),P2+vec2(-10,-40),P2+vec2(10,40))) {Go=DL,Boost+=0.1}
if (Go==UR&inrange(Ball+vec2(0,13),P2+vec2(-10,-40),P2+vec2(10,40))) {Go=DL,Boost+=0.1}
if (Go==DR&inrange(Ball+vec2(0,-13),P2+vec2(-10,-40),P2+vec2(10,40))) {Go=UL,Boost+=0.1}
EGP:egpPos(1,Go*Move*Boost+Ball)
#if (Boost>5) {Boost=5}

#This Part Makes The Score Thing And The Speed Thing
if (Ball:x()<-100) {EGP:entity():soundPlay(1,1,"buttons/bell1.wav"),EGP:egpPos(1,vec2(256,256))
    Score1++,Go=Dirs[randint(1,4),vector2],Boost=1.5}
if (Ball:x()>600) {EGP:entity():soundPlay(1,1,"buttons/bell1.wav"),EGP:egpPos(1,vec2(256,256))
    Score2++,Go=Dirs[randint(1,4),vector2],Boost=1.5}
if (changed(Boost)) {EGP:egpSetText(10,"Speed: "+(Boost/5*100):toString()+"%")}
if (changed(Score1)) {EGP:egpSetText(8,"Player 1's Score: "+Score1:toString())}
if (changed(Score2)) {EGP:egpSetText(9,"Player 2's Score: "+Score2:toString())}

#This Part Makes The Pedals Move
if (Pod1["Active",number]) {
    if (Pod1["W",number]&P1:y()>50) {W1=-2} else {W1=0}
    if (Pod1["S",number]&P1:y()<460) {S1=2} else {S1=0}
    if (Pod1["A",number]&P1:x()>30) {A1=-2} else {A1=0}
    if (Pod1["D",number]&P1:x()<200) {D1=2} else {D1=0}
    EGP:egpPos(2,P1+vec2(A1+D1,W1+S1)*(Boost-0.5)*Move)
    } else {
    if (P1:y()>50&Ball:y()<P1:y()) {W1=-1} else {W1=0}
    if (P1:y()<460&Ball:y()>P1:y()) {S1=1} else {S1=0}
    EGP:egpPos(2,P1+vec2(0,(W1+S1)*Boost*Move))
    }
    
if (Pod2["Active",number]) {
    if (Pod2["W",number]&P2:y()>50) {W2=-2} else {W2=0}
    if (Pod2["S",number]&P2:y()<460) {S2=2} else {S2=0}
    if (Pod2["A",number]&P2:x()>280) {A2=-2} else {A2=0}
    if (Pod2["D",number]&P2:x()<480) {D2=2} else {D2=0}
    EGP:egpPos(3,P2+vec2(A2+D2,W2+S2)*(Boost-0.5)*Move)
    } else {
    if (P2:y()>50&Ball:y()<P2:y()) {W2=-1} else {W2=0}
    if (P2:y()<460&Ball:y()>P2:y()) {S2=1} else {S2=0}
    EGP:egpPos(3,P2+vec2(0,(W2+S2)*Boost*Move))
    }

#ColorFul Pedals :)
if (changed(Pod1["Mouse1",number])&Pod1["Mouse1",number]) {EGP:egpColor(2,randvec(0,255))}
if (changed(Pod2["Mouse1",number])&Pod2["Mouse1",number]) {EGP:egpColor(3,randvec(0,255))}

#Help Hints
if (changed(Pod1["Active",number])&Pod1["Active",number]) {Pod1["Entity",entity]:hintDriver("Use W,A,S,D To Move",7)
    Pod1["Entity",entity]:hintDriver("And Mouse 1 To Do The Magic :)",7)}
if (changed(Pod2["Active",number])&Pod2["Active",number]) {Pod1["Entity",entity]:hintDriver("Use W,A,S,D To Move",7)
    Pod1["Entity",entity]:hintDriver("And Mouse 1 To Do The Magic :)",7)}

if (changed(EGP:entity())&EGP:entity()) {reset()}
