@name Bezier Curve
@outputs Point:vector T:number Time:number Points:array PointsClone:array
@persist User:entity
@trigger all

interval(100)

if(first()){
    User = entity():owner()
    holoCreate(0, entity():pos())
    holoColor(0, vec(0,0,255))
}

# Point set
if(User:keyUse()){
    Points:pushVector(User:shootPos())
    foreach(K, PlacePoint:vector=Points){
        holoCreate(K+1, PlacePoint)
    }
}
#Points clear
if(User:keyReload()){
    foreach(K, PlacePoint:vector=Points){
        holoDelete(K+1)
    }
    Points = array()
}

# Bezier
PointsClone = Points:clone()
while(PointsClone:count()>1){
    NewPoints = array()
    for(I = 1, PointsClone:count() - 1){
        NewPoint = (1-T) * PointsClone:vector(I) + T * PointsClone:vector(I+1)
        NewPoints:pushVector(NewPoint)
    }
    PointsClone = NewPoints
}
Point = PointsClone:vector(1)

# Point Animation
Time+=5
T=(sin(Time) + 1)/2
holoPos(0, Point)
