//AutoMeter Phantom Speedometer by Field Medic
//AutoMeter is a registered trademark of AUTOMETER PRODUCTIONS
//Port 0 is speed in MPH

dclr;  //Clear screen

mov #65485,20;  // Circle quality register

dcolor blanco;
dcircle center,202;
dcolor naranja;
dcircle center,22;
dcolor negro;
dcircle screw1,10;
dcircle screw2,10;
dsetsize 60;
dwrite unitpos,units;
dsetsize 50;
dwritei twenty,20;
dwritei forty,40;
dwritei sixty,60;
dwritei eighty,80;
dwritei onehundred,100;
dsetsize 38;
dwritei onetwenty,120;
dwritei zero,0;
dsetsize 16;
dwritei ten,10;
dwritei thirty,30;
dwritei fifty,50;
dwritei seventy,70;
dwritei ninety,90;
dwritei oneten,110;
dwrite companypos,company;

dcolor tan;
drectwh odometerpos,odometer;

dcolor negro;
dmove center;
mov ebx,-1.18;
drawloop:
add ebx,0.39;
drotatescale ebx,1;
drectwh dashpos,dash;
cmp ebx,3.5;
jle drawloop;

in eax,0;
dcolor naranja;
div eax,120; //Divide by top speed (180 KPH)
mul eax,4.7;  //Multiply by 270 degrees (how far our gauge goes)
add eax,5.49; //Cheat and add a few to get the needle to land in the right place
drotatescale eax,1;
dvxpoly needle,4;

dexit;

//Vectors
vector2f center,256,256;  // center as reference
vector2f dashpos,-5,-188;
vector2f dash,10,34;
vector2f zero,140,332;
vector2f twenty,108,236;
vector2f forty,134,150;
vector2f sixty,222,104;
vector2f eighty,304,150;
vector2f onehundred,314,232;
vector2f onetwenty,330,324;
vector2f unitpos,196,368;
vector2f ten,120,302;
vector2f thirty,120,194;
vector2f fifty,186,120;
vector2f seventy,300,120;
vector2f ninety,372,194;
vector2f oneten,364,296;
vector2f odometer,164,38;
vector2f odometerpos,168,338;
vector2f companypos,202,318;
vector2f screw1,210,256;  // Screw one
vector2f screw2,302,256; // Screw two

//Colors
color blanco,230,230,230;  // Autometer "white"
color negro,0,0,0;   // Black
color naranja,227,85,39; // Autometer "orange"
color tan,141,138,104; // Autometer odometer background

//Strings
string units,'MPH';
string company,'AutoMeter';

//Polygons

needle:
db -5,-160;
db 5,-160;
db 10,44;
db -10,44;
