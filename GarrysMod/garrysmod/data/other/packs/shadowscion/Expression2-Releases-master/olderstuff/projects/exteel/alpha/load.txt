@name exteel/alpha/load
#----------------------------------------------------------------
@inputs Camera:wirelink [W A S D] [Mouse1 Mouse2] [Shift Space] [Active] [Entity]:entity


#----------------------------------------------------------------
@outputs Ops Cpu


#-- General variables --------------------------------------------
@persist [G_Dt G_Ct G_Ts]


#-- Physics variables -------------------------------------------
@persist [P_MoveDir P_Position P_Velocity P_BBoxMins P_BBoxMaxs]:vector [P_Angle]:angle [P_Entity]:entity
@persist [P_HitBBox P_HitCeiling P_HitGround P_EnableAirStrafe P_JumpHeight P_JumpSpeed P_BBoxHeight P_BBoxHeightReal P_BBoxRadius]


#-- Animation variables -----------------------------------------
@persist [A_InputBlend]


#----------------------------------------------------------------
@trigger none


#----------------------------------------------------------------
if (first()) {
    #include "exteel/alpha/model"
    #include "exteel/alpha/anim"

    timer("spawn",100)
}


#----------------------------------------------------------------
if (clk("spawn")) {
    local Done = spawn_holograms(0,1)
    if (Done) {
        timer("main",90)


        #-- Model settings
        L_A = 45
        L_B = 52
        L_W = 22
        L_Total = L_A + L_B

        A_WalkPelvisZ = L_Total*0.95
        A_WalkStrideX = L_B
        A_WalkStrideZ = L_A/2
        A_WalkStrideY = vec(0,L_W*1.1,0)

        A_RunPelvisZ = A_WalkPelvisZ
        A_RunStrideY = A_WalkStrideY/2

        A_WalkMoveSpeed = A_WalkStrideX*2*_PI
        A_RunMoveSpeed = A_RunPelvisZ*2*_PI


        #-- Physics settings
        P_JumpHeight      = 100
        P_BBoxHeightReal  = A_WalkPelvisZ
        P_BBoxRadius      = 20


        #-- Physics variables and setup
        P_Entity = Root = holoEntity(9999),holoUnparent(9999)

        P_BBoxHeight = P_BBoxHeightReal*0.5
        P_BBoxMins = -vec(P_BBoxRadius,P_BBoxRadius,P_BBoxHeight)
        P_BBoxMaxs = vec(P_BBoxRadius,P_BBoxRadius,0)
        P_Position = P_Entity:pos()
        P_Velocity = vec()

        Camera["Parent",entity] = P_Entity
        Camera["Distance",number] = 100
        Camera["Position",vector] = vec()


        #-- General variables
        G_WorldZ = vec(0,0,1)
        G_Ct = curtime()
    }
}


#----------------------------------------------------------------
if (clk("main")) {

    #-- Timers
    timer("main",90)

    G_Ts = 1                            #time scale
    G_Dt = (curtime() - G_Ct)*G_Ts      #delta time
    G_Ct = curtime()                    #current time


    if (Active) {
        #-- Player and inputs
        Camera["Activated",number] = 1

        A_InputBlend += clamp((S ? (S|A|D) : (W|A|D)*2)*0.5 - A_InputBlend,-G_Dt*0.5,G_Dt)

        local P_EyeDir = Entity:toLocalAxis(Entity:driver():eye()):setZ(0)
        if (A_InputBlend) {
            P_Angle = P_EyeDir:toAngle()
        }
        if (W|S) {
            P_MoveDir = (W - S)*P_EyeDir
        }
        if (A|D) {
            P_MoveDir += P_EyeDir:cross(G_WorldZ)*(D - A)
        }
        P_MoveDir = P_MoveDir:normalized()

        local C_Speed = (A_LayerOutput["nmovespeed",number]*A_LayerOutput["nframerate",number])*G_Dt #-- grab novespeed from blended anim
        local C_Gravity = -600*G_Dt*G_Dt


        #-- Jump and gravity
        if (P_HitGround) {
            P_Velocity = (P_MoveDir*C_Speed):setZ(Space*sqrt(2*abs(C_Gravity)*P_JumpHeight))
            if (P_Velocity[3]) {
                P_JumpSpeed = P_Velocity:setZ(0):length()
                P_HitGround = 0
            }
        }
        else {
            P_Velocity += vec(0,0,C_Gravity)
        }


        #-- XYAxis velocity
        P_HitBBox = 0

        local BBoxTrace = rangerOffsetHull(P_Position,P_Position + P_Velocity,P_BBoxMins,P_BBoxMaxs)
        if (BBoxTrace:hit()) {
            local CheckPos = P_Position + P_Velocity
            local CheckHit = BBoxTrace:position()
            local CheckDir = BBoxTrace:hitNormal():setZ(0)*1.05

            BBoxTrace = rangerOffsetHull(CheckHit,CheckPos - CheckDir*(CheckPos - CheckHit):dot(CheckDir),P_BBoxMins,P_BBoxMaxs)

            P_Velocity = (BBoxTrace:position() - P_Position):setZ(P_Velocity[3])
            P_HitBBox = 1
        }


        #-- ZAxis velocity
        local UpDown = sign(P_Velocity[3]) ?: -1
        local GTrace = rangerOffset(9999999,P_Position + P_Velocity:setZ(0),vec(0,0,UpDown))

        if (P_HitGround) {
            local GDot = P_Velocity:normalized():setZ(0):dot(GTrace:hitNormal())
            if (GDot > 0) {
                P_Position[3] = GTrace:position()[3] + P_BBoxHeightReal
                P_Velocity[3] = 0
                P_HitGround = 2
            }
        }
        if (UpDown == -1) {
            local Ground = GTrace:position()[3] + P_BBoxHeightReal

            P_Position[3] = max(P_Position[3] + P_Velocity[3],Ground)
            if (P_Position[3] <= Ground) {
                P_Position[3] = Ground
                P_Velocity[3] = 0
                P_HitGround = 1
            }
            else {
                P_HitGround = 0
            }
        }
        else {
            local Ceiling = GTrace:position()[3] - P_Velocity[3]

            P_Position[3] = min(P_Position[3],Ceiling)
            if (P_Position[3] >= Ceiling) {
                P_Position[3] = Ceiling
                P_Velocity[3] = C_Gravity
                P_HitCeiling = 1
            }
            else {
                P_HitCeiling = 0
            }
        }


        #-- Integrate velocity
        P_Position += P_Velocity

        holoPos(9999,P_Position)
        holoAng(9999,P_Angle)


        #-- Animation overlay
        A_FrameLoop += A_LayerOutput["nframerate",number]*360*G_Dt

        A_STATE_Direction = atan(P_MoveDir[2],P_MoveDir[1]) - atan(P_EyeDir[2],P_EyeDir[1])
        A_STATE_Speed = A_InputBlend

        A_LayerOutput = blendspaceNormalMotion()

        holoPos(9998,A_LayerOutput["vpelvispos",vector])
        holoAng(9998,A_LayerOutput["qpelvisang",quaternion]:toAngle())

        quat_ik(12,13,14,L_A,L_B,holoEntity(9998),A_LayerOutput["vfootrpos",vector],A_STATE_Speed )
        quat_ik(16,17,18,L_A,L_B,holoEntity(9998),A_LayerOutput["vfootlpos",vector],A_STATE_Speed )

    }
    else {
        Camera["Activated",number] = 0
    }

    Ops = ops()
    Cpu = cpuUsage()*1000000
}
